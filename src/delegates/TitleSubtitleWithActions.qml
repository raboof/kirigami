/*
 * SPDX-FileCopyrightText: 2010 Marco Martin <notmart@gmail.com>
 * SPDX-FileCopyrightText: 2022 ivan tkachenko <me@ratijas.tk>
 * SPDX-FileCopyrightText: 2023 Arjen Hiemstra <ahiemstra@heimr.nl>
 * SPDX-FileCopyrightText: 2025 Akseli Lahtinen <akselmo@akselmo.dev>
 *
 * SPDX-License-Identifier: LGPL-2.0-or-later
 */

import QtQuick
import QtQuick.Templates as T
import QtQuick.Layouts
import org.kde.kirigami as Kirigami

/*!
 \ *qmltype TitleSubtitleWithActions
 \inqmlmodule org.kde.kirigami.delegates

 \brief A simple title delegate that has trailing actions.

 This is meant to be used in lists for items that have actions.
 For example lists of usernames, with any related actions after them,
 such as rename and delete actions.

 Example usage as contentItem of an ItemDelegate:

\qml
ItemDelegate {
    id: itemDelegate
    icon: "user"
    text: i18nc("@title:row", "Konqi")
    readonly property string subtitle: i18nc("@label", "The Konqueror")
    Accessible.description: subtitle
    Kirigami.Theme.useAlternateBackgroundColor: true

    onClicked: [...]

    contentItem: Kirigami.TitleSubtitleWithActions {
        title: itemDelegate.title
        subtitle: itemDelegate.subtitle
        elide: Text.ElideRight
        selected: itemDelegate.pressed || itemDelegate.highlighted
        actions: [
            Kirigami.Action {
                icon.name: "edit-entry-symbolic"
                text: i18nc("@action:button", "Modify user…")
                onTriggered: [...]
                tooltip: text
            },
            Kirigami.Action {
                icon.name: "edit-delete-remove-symbolic"
                text: i18nc("@action:button", "Remove user…")
                onTriggered: [...]
                tooltip: text
                displayHint: Kirigami.DisplayHint.IconOnly
            }
        ]
    }
}
\endqml

\sa IconTitleSubtitle
\sa TitleSubtitle
\sa ActionToolBar
*/

Item {
    id: root

    /*!
     \ *qmlproperty list<Action> ActionToolBar::actions

     \brief This property holds a list of visible actions.

     These actions will be given to ActionToolBar.
     If you want some of the actions to be icons-only, you will
     have to set them individually.

     List of actions is empty by default.

         \sa ActionToolBar
     */
    property list<T.Action> actions

    /*!
        The title to display.
     */
    required property string title

    /*!
        The subtitle to display.
        Empty by default, so subtitle is not shown.

        default: Empty \c string
     */
    property string subtitle

    /*!
        Should this item be displayed in a selected style?

        default: \c false
     */
    property bool selected: false

    /*!
        The text elision mode used for both the title and subtitle.

        default: \c Text.ElideRight
     */
    property var elide: Text.ElideRight

    /*!
     This property determines how the icon and text are displayed within the button.

     Permitted values are:
     \list
     \li Button.IconOnly
     \li Button.TextOnly
     \li Button.TextBesideIcon
     \li Button.TextUnderIcon
     \endlist

     default: \c Controls.Button.TextBesideIcon

         \sa ActionToolBar
         \sa AbstractButton
    */
    property alias displayHint: actionToolBar.display

    implicitWidth: layout.implicitWidth
    implicitHeight: layout.implicitHeight

    RowLayout {
        id: layout
        anchors.fill: root
        spacing: Kirigami.Units.smallSpacing

        Kirigami.TitleSubtitle {
            id: titleSubtitle
            Layout.fillWidth: true
            Layout.alignment: Qt.AlignLeft
            Layout.horizontalStretchFactor: 2
            title: root.title
            subtitle: root.subtitle
            elide: root.elide
            selected: root.selected
        }

        Kirigami.ActionToolBar {
            id: actionToolBar
            Layout.fillWidth: true
            Layout.fillHeight: true
            Layout.horizontalStretchFactor: 1
            actions: root.actions
            alignment: Qt.AlignRight
            flat: false
        }
    }
}
